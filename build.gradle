apply from: 'jpos/libraries.gradle'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'pmd'

    group = 'org.jpos'
    version = '2.0.1-SNAPSHOT'
    [ compileJava, compileTestJava, javadoc ]*.options*.encoding = 'UTF-8'
    def isSnapshot = version.contains("SNAPSHOT")
    def mavenCentralRepo = isSnapshot ?
        'https://oss.sonatype.org/content/repositories/snapshots/' :
        'https://oss.sonatype.org/service/local/staging/deploy/maven2';

    configurations.compile.transitive = true
    javadoc.failOnError = false
    pmd.ignoreFailures = true

    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://download.oracle.com/maven' }
        maven { url 'http://jline.sourceforge.net/m2repo' }
    }
    if (project.hasProperty("lint")) {
      // gradle -Plint ...
      tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint" 
      } 
    }

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
          options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    def pomConfig = {
        name 'jPOS Project'
        description """
    jPOS is an ISO-8583 based financial transaction 
    library/framework that can be customized and 
    extended in order to implement financial interchanges.
  """
        url 'http://jpos.org'
        inceptionYear '1998'
        organization {
            name 'jPOS.org'
            url 'http://jpos.org'
        }
        issueManagement {
            system 'YouTrack'
            url 'https://jpos.org/issues'
        }
        scm {
            url "http://github.com/jpos/jPOS"
            connection "scm:git:https://github.com/jpos/jPOS.git"
            developerConnection "scm:git:git@github.com:jpos/jPOS.git"
        }
        licenses {
            license {
                name 'GNU AFFERO GENERAL PUBLIC LICENSE'
                url 'http://www.gnu.org/licenses/agpl-v3.html'
                comments 'See http://jpos.org/license for more details.'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'jpos-team'
                name 'jPOS Development Team'
                email 'info@jpos.org'
            }
        }
    }
    signing {
        required { !isSnapshot }
        sign configurations.archives
    }
    configure(install.repositories.mavenInstaller) {
        pom.project pomConfig
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project pomConfig
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                /*
                repository(url: mavenCentralRepo) {
                    authentication(userName: mavenCentralUsername, password: mavenCentralPassword)
                }
                */
                repository(url: 'file:///opt/local/maven')
            }
        }
    }

}

// Configure IDEA to use Git
idea.project.ipr {
   withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}

task aggregatedJavadoc (type: Javadoc, description: "Aggregated Javadocs") {
    source subprojects.collect {project ->
        project.sourceSets.main.allJava
    }
    destinationDir = new File(buildDir, 'docs/javadoc')
    classpath = files(subprojects.collect {project ->
        project.sourceSets.main.compileClasspath})
    failOnError = false
}

task wrapper( type: Wrapper ) {
    gradleVersion = '2.2.1'
}

