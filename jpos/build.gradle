plugins {
   id "org.gradlex.extra-java-module-info" version "1.4.1"
   id "org.gradlex.java-module-dependencies" version "1.6"
   id "org.owasp.dependencycheck" version "8.3.1"

   // id 'checkstyle'
}

extraJavaModuleInfo {
    failOnMissingModuleInfo.set(false)
    def sshdVersion = ((String) libs.sshd.orNull).split(":")[-1] // there has to be a better way to get the version

    automaticModule ('org.jdom:jdom2', 'org.jdom2')
    automaticModule ('commons-cli:commons-cli', 'org.apache.commons.cli')
    automaticModule ('org.javatuples:javatuples', 'org.javatuples')
    automaticModule ('org.hdrhistogram:HdrHistogram', 'org.hdrhistogram.HdrHistogram')
    automaticModule ('org.jline:jline', 'org.jline')
    automaticModule ('org.apache-extras.beanshell:bsh', 'bsh')
    automaticModule ('com.sleepycat:je', 'com.sleepycat.je')
    automaticModule ('jdbm:jdbm', 'org.jdbm')
    automaticModule ('io.micrometer:micrometer-core', "micrometer.core")
    automaticModule ('io.micrometer:micrometer-registry-prometheus', "micrometer.registry.prometheus")

    module ('org.apache.sshd:sshd-core',  'org.apache.sshd', "${sshdVersion}") {
        mergeJar('org.apache.sshd:sshd-common')
        mergeJar('net.i2p.crypto:eddsa')

        exports('org.apache.sshd.server')
        exports('org.apache.sshd.server.auth.pubkey')
        exports('org.apache.sshd.server.session')
        exports('org.apache.sshd.server.channel')
        exports('org.apache.sshd.server.command')
        exports('org.apache.sshd.server.shell')

        exports('org.apache.sshd.server.keyprovider')
        exports('org.apache.sshd.common')
        exports('org.apache.sshd.common.config.keys')
        exports('org.apache.sshd.common.util.security.eddsa')
        exports('org.apache.sshd.common.cipher')
        exports('org.apache.sshd.common.channel')
        exports('org.apache.sshd.common.keyprovider')
        exports('org.apache.sshd.common.util.buffer')
        exports('org.apache.sshd.common.util.security')
        exports('org.apache.sshd.common.config.keys.impl')
        exports('net.i2p.crypto.eddsa')
        exports('net.i2p.crypto.eddsa.spec')
    }
}

dependencies { 
    api libs.jdom
    api libs.javatuples
    api libs.jline
    api libs.jna
    api libs.jansi
    api libs.beanshell
    api libs.bcprov
    api libs.bcpg
    api libs.commonscli
    api libs.slf4japi
    api libs.micrometercore
    api libs.micrometerprometheus
    api libs.jacksonCore
    api libs.jacksonDataBind
    api libs.jacksonDataTypeJSR310
    api libs.jacksonDataFormatXML
    
    implementation libs.yaml;
    implementation libs.jdbm
    implementation libs.sleepycatje
    implementation libs.sshd
    implementation libs.eddsa
    implementation libs.hdrhistogram

    testImplementation libs.commonslang3
    testImplementation libs.hamcrest
    testImplementation libs.xmlunit
    testImplementation libs.junit
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation (libs.mockito) {
        exclude(module: 'hamcrest-core')
    }
    testImplementation libs.mockitojupiter

    //  JSONPackager on hold
    //    compile (libraries.jsonsimple) {
    //        exclude(module: 'junit')
    //        exclude(module: 'hamcrest-core')
    //    }
    // Integrated OSGI framework
    // compile 'org.apache.felix:org.apache.felix.framework:5.0.0'
}

def q2ArchiveJarName="${project.name}-${project.version}-q2.jar"
def jposCopySpec = copySpec {
    def cfg = new Properties()
    def target = project.hasProperty('target') ? target : 'devel'
    cfg.put('jarname', q2ArchiveJarName.toString())
    cfg.put('target', target.toString())
    File cfgFile = file("${target}.properties")
    if (cfgFile.exists()) {
        cfgFile.withInputStream{
            cfg.load(it);   
        }
    }
    from(file("src/dist")) {
        exclude 'cfg/*.lmk'
        exclude 'cfg/*.jks'
        exclude 'cfg/*.ks'
        exclude 'cfg/*.ser'
        exclude 'cfg/authorized_keys'
        filter(
            org.apache.tools.ant.filters.ReplaceTokens, 
            tokens: cfg
        )
    }
    from(file("src/dist")) {
        include 'cfg/*.lmk'
        include 'cfg/*.ks'
        include 'cfg/*.jks'
        include 'cfg/*.ser'
        include 'cfg/authorized_keys'
    }
    from(file("${project.buildDir}/libs/${q2ArchiveJarName}"))
    into("lib") {
        from(configurations.runtimeClasspath)
    }
    /*
    filePermissions {
       user {
         read = true
         write = false
         execute = false
       }
       group {
         read = false
         write = false
         execute = false
       }
       other {
         read = false
         write = false
         execute = false
       }
    }
    */
}

task listJars {
    doLast {
        configurations.testCompile.each { File file -> println file.name }
        println "    libsDir: ${project.libsDir}"
        println "libsDirName: ${project.libsDirName}"
        println "libsDirName: ${project.libsDirName}"
        println "    project: ${project.buildDir}"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'jPOS', 
                   'Implementation-Version': project.version
    }
}

java {
   withSourcesJar()
}

task q2jar (type: Jar) {
    dependsOn jar
    archiveClassifier.set('q2')
    from sourceSets.main.output.classesDirs, sourceSets.main.output.resourcesDir
    manifest {
        def manifestClasspath = configurations.runtimeClasspath.collect { "lib/" + it.getName() }.join(' ') 
        attributes 'Implementation-Title': 'jPOS', 
                   'Implementation-Version': project.version,
                   'Main-Class': 'org.jpos.q2.Q2',
                   'Class-Path': manifestClasspath
    }
}

//checkstyle {
//    ignoreFailures = true
//    configFile file("${rootProject.projectDir}/.github/linters/sun_checks.xml")
//}

task sourceJar( type: Jar ) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar, javadocJar
}

task dist (type: Tar) { 
    dependsOn 'jar', 'assemble', 'sourceJar'
    compression = Compression.GZIP
    includeEmptyDirs true
    with jposCopySpec
    into "jpos-$project.version"
    archiveExtension="tar.gz"
}

task zip (type: Zip) { 
    dependsOn 'q2jar', 'assemble', 'sourceJar'
    includeEmptyDirs true
    includeEmptyDirs true
    with jposCopySpec
    into "jpos-$project.version"
}

task version (type: JavaExec, dependsOn: classes) {
    description = "Display jPOS Version"
    mainClass = 'org.jpos.q2.Q2'
    args = "--version".split().toList()
    classpath sourceSets.main.runtimeClasspath
}

// just an alias to keep the old install task name
task install(dependsOn: publishToMavenLocal) {}


class GitRevisionTask extends DefaultTask
{
    @InputFile @Optional
    File gitHead

    @Input 
    boolean gotHead

    @InputFile @Optional
    File getRefFile() {
        File rf = new File(gitHead.parent,gitHead.text.replace('ref: ', '').trim())
        return rf.exists() ? rf : gitHead
    }

    @OutputFile
    File outputFile

    @TaskAction
    public void writeFile()
    {
        Properties props=new Properties()
        if (gotHead) {
            File ref=getRefFile()
            if (ref != null && ref.exists()) {
                props.put("branch",ref.getName())
                props.put("revision",ref.text.substring(0,7))
            } else {
                props.put("branch", "detached");
                props.put("revision", gitHead.text.substring(0,7))
            }
        } else {
            props.put("branch", "unknown");
            props.put("revision", "unknown");
        }
        props.store(new FileOutputStream(outputFile),"Revision Properties")
    }
}

class BuildTimestampTask extends DefaultTask {
    @OutputFile
    File outputFile

    @TaskAction
    public void writeFile() {
        new File(outputFile.parent).mkdirs()
        Properties props=new Properties()
        props.put("version", project.version);
        props.put("buildTimestamp", new Date().format("yyyy-MM-dd HH:mm:ss z"));
        props.store(new FileOutputStream(outputFile),"Revision Properties")
    }
}

task createRevisionPropertyFile(type: GitRevisionTask) {
    gitHead = "$rootDir/.git/HEAD" as File
    gotHead = gitHead.exists()
    if (!gotHead)
        gitHead = null;
    outputFile = "$sourceSets.main.output.resourcesDir/org/jpos/q2/revision.properties" as File
}

task createBuildTimestampPropertyFile(type: BuildTimestampTask) {
    outputFile = "$sourceSets.main.output.resourcesDir/org/jpos/q2/buildinfo.properties" as File
}

processResources.dependsOn createBuildTimestampPropertyFile, createRevisionPropertyFile
processResources.outputs.upToDateWhen{ false } // issue with Gradle 2.4 confused about UP-TO-DATE (works OK with 2.3)

task installApp(type: Sync) {
    dependsOn q2jar
    delete "${project.buildDir}/install/${project.name}/cfg"
    description 'Installs jPOS based application'
    into { file("${project.buildDir}/install/${project.name}") }
    with jposCopySpec
}

task viewTests (description: 'Open Test Reports') {
    doLast {
        java.awt.Desktop.desktop.browse (
            new File("${buildDir}/reports/tests/test", 'index.html').toURI()
        )
    }
}


test {
  minHeapSize = "4G" 
  maxHeapSize = "8G"
}

tasks.withType(JavaCompile) {
    options.deprecation = false
}

tasks.withType(Test) {
  jvmArgs("-XX:+EnableDynamicAgentLoading")
}

